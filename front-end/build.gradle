apply plugin: 'com.moowork.node'

description = 'Angular Front End'

def angularSrcPath = "src/main/angular"
def angularDistPath = "$angularSrcPath/dist"
def nodeModulesPath = "$angularSrcPath/node_modules"

node {
    version = '10.13.0'
    npmVersion = '6.4.1'
    download = true
    nodeModulesDir = file(nodeModulesPath)
}

npmInstall { 
    workingDir = file(angularSrcPath)
    outputs.dir nodeModulesPath 
}

task npmBuild(type: NpmTask) {
    group = 'node'
    dependsOn npmInstall
    outputs.dir angularDistPath
    args = ['run', 'build']
}

task npmStart(type: NpmTask) {
    group = 'node'
    dependsOn npmInstall
    args = ['start']
}

task npmStartProxy(type: NpmTask) {
    group = 'node'
    dependsOn npmInstall
    args = ['run-script', 'start-proxy']
}

task distClean {
    group = 'node'
    dependsOn clean
    doLast { delete angularDistPath }
}

task nodeClean {
    group = 'node'
    dependsOn clean
    doLast { delete nodeModulesPath, '.gradle' }
}

task npmClean {
    group = 'node'
    dependsOn nodeClean, distClean
}

sourceSets.main.resources { srcDirs += angularDistPath }

jar { dependsOn npmBuild }